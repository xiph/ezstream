dnl INITIALIZATION

AC_INIT([ezstream], [0.3.0], [grmo@users.sourceforge.net])
AC_PREREQ(2.61)
AC_CONFIG_SRCDIR(src/ezstream.c)
AM_INIT_AUTOMAKE
AC_CONFIG_HEADERS(src/config.h)
AC_CONFIG_LIBOBJ_DIR(src)
AM_MAINTAINER_MODE
AC_USE_SYSTEM_EXTENSIONS


dnl SETUP

AC_CANONICAL_HOST

DEBUG="-g"
if test -z "$GCC"; then
    XIPH_CPPFLAGS="-D_REENTRANT"
    case $host in 
        *-*-irix*)
                XIPH_CPPFLAGS="$XIPH_CPPFLAGS -w -signed"
                PROFILE="-p -g3 -O2 -signed -D_REENTRANT"
                ;;
        *-*-solaris*)
                XIPH_CFLAGS="-xO4 -xcg92"
                XIPH_CPPFLAGS="$XIPH_CPPFLAGS -v -w -fsimple -fast"
                PROFILE="-xpg -g -Dsuncc"
                ;;
        *)
                XIPH_CFLAGS="-O"
                PROFILE="-g -p"
                ;;
    esac
else
    XIPH_CPPFLAGS="-Wall -ffast-math -fsigned-char"
    PROFILE="-pg -g"
fi


dnl MISC SYSTEM CHARACTERISTICS

dnl __progname check adapted from OpenNTPd-portable's configure.ac
AC_MSG_CHECKING([whether libc defines __progname])
AC_LINK_IFELSE(
	[AC_LANG_PROGRAM([[#include <stdio.h>]],
		[[extern char *__progname; printf("%s\n", __progname); ]])],
	[ac_cv_libc_defines___progname="yes"],
	[ac_cv_libc_defines___progname="no"]
)
if test x"$ac_cv_libc_defines___progname" = "xyes"; then
	AC_MSG_RESULT([yes])
	AC_DEFINE(HAVE___PROGNAME, 1, [Define whether libc defines __progname])
else
	AC_MSG_RESULT([no])
fi

AC_C_CONST
AC_TYPE_SIZE_T


dnl USEFUL HEADERS

AC_CHECK_HEADERS(sys/time.h paths.h signal.h)


dnl LIBRARY FUNCTIONS

AC_CHECK_FUNCS(arc4random gettimeofday random srandomdev)
AC_REPLACE_FUNCS(getopt strlcat strlcpy)
if test x"$ac_cv_header_signal_h" = "xyes"; then
	AC_CHECK_FUNCS([sigaction], [
		AC_DEFINE(HAVE_SIGNALS, 1, [Define whether we have BSD signals])
		], [], [#include <signal.h>])
fi


dnl CONFIGURE OPTIONS

XIPH_PATH_SHOUT(, AC_MSG_ERROR([must have libshout installed!]))
if test "$SHOUT_THREADSAFE" != "yes"
then
  AC_MSG_ERROR([This libshout isn't threadsafe])
fi 

XIPH_PATH_XML

XIPH_VAR_APPEND([XIPH_CFLAGS], [$XML_CFLAGS])
XIPH_VAR_PREPEND([XIPH_LIBS], [$XML_LIBS])

XIPH_VAR_APPEND([XIPH_CPPFLAGS], [$SHOUT_CPPFLAGS])
XIPH_VAR_APPEND([XIPH_CFLAGS], [$SHOUT_CFLAGS])
XIPH_VAR_PREPEND([XIPH_LIBS], [$SHOUT_LIBS])

XIPH_PATH_VORBIS(, AC_MSG_ERROR([must have Ogg Vorbis v1.0 installed!]))
XIPH_VAR_APPEND([XIPH_CPPFLAGS],[$VORBIS_CFLAGS $VORBISFILE_CFLAGS])
XIPH_VAR_PREPEND([XIPH_LIBS],[$VORBIS_LIBS $VORBISFILE_LIBS])


dnl OUTPUT

AC_CONFIG_FILES(Makefile \
		conf/Makefile \
		doc/Makefile \
		src/Makefile \
		win32/Makefile)

AC_SUBST(XIPH_CPPFLAGS)
AC_SUBST(XIPH_CFLAGS)
AC_SUBST(XIPH_LIBS)
AC_SUBST(LIBS)
AC_SUBST(DEBUG)
AC_SUBST(CFLAGS)
AC_SUBST(PROFILE)

AC_OUTPUT
